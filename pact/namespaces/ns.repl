(env-exec-config ["DisablePact44"])
(begin-tx)
(env-data
 { 'ns-admin-keyset: ["admin"]
 , 'ns-operate-keyset: ["operate"]
 , 'ns-genesis-keyset: { "keys": [], "pred": "="} })

(load "v1/ns.pact")
(load "ns.pact")
(commit-tx)

(env-namespace-policy false (ns.validate))

(begin-tx)
(namespace 'user)
(env-keys [])

(module mod G
  (defcap G () (enforce false "disabled"))
  (defun foo () 1))

(namespace 'free)

(module mod G
  (defcap G () (enforce false "disabled"))
  (defun foo () 2))

(expect-failure
  "Cannot bring kadena ns into scope w/o operate admin"
  (namespace 'kadena))

(env-keys ["operate"])

(namespace 'kadena)

(module mod G
  (defcap G () (enforce false "disabled"))
  (defun foo () 3))

(commit-tx)

(expect "user.mod works" 1 (user.mod.foo))
(expect "free.mod works" 2 (free.mod.foo))
(expect "kadena.mod works" 3 (kadena.mod.foo))

(begin-tx)
(env-keys ["operate"])
(env-data
 { 'ns-admin-keyset: ["admin"]
 , 'ns-operate-keyset: ["operate"] })

(expect-failure "cannot redefine user"
  (define-namespace 'user ns.GUARD_FAILURE ns.GUARD_FAILURE))
(expect-failure "cannot redefine free"
  (define-namespace 'free ns.GUARD_FAILURE ns.GUARD_FAILURE))
(expect "can redefine kadena"
  "Namespace defined: kadena"
  (define-namespace 'kadena ns.GUARD_SUCCESS ns.GUARD_FAILURE))

(commit-tx)

(begin-tx)
(env-keys [])

(namespace 'kadena)

(module mod2 G
  (defcap G () (enforce false "disabled"))
  (defun foo () 4))
(commit-tx)
(begin-tx)

(expect "kadena.mod2 works" 4 (kadena.mod2.foo))

(use ns)
(env-keys ["operate"])
(expect-failure
  "cannot register empty name"
  (write-registry "" GUARD_SUCCESS true))

(expect-failure
  "cannot register >64 length name"
  (write-registry
    "1234567890123456789012345678901234567890123456789012345678901234567890"
    GUARD_SUCCESS true))

(expect-failure
  "must be latin1 charset"
  (write-registry "emilyÏ€" GUARD_SUCCESS true))

(commit-tx)
(begin-tx)

(env-exec-config [])
(env-data
 { 'single :
   ["70c787fcfe6c6f4ec23d13c2e94682bc90952f7cec06c7dbac1c012b0b6678b9"]
 , 'multi : ["a", "b"]
 })

(expect-failure
  "Principal of other than admin keyset fails"
  "Inactive or unregistered namespace"
  (define-namespace
   "D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4"
   (read-keyset 'single)
   (read-keyset 'multi))
)

(define-namespace
  "D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4"
  (read-keyset 'single)
  (read-keyset 'single))

(define-namespace
  "dDxw31bRij5gA0Xvl_uDUOZMvrnkMscu-Ax-H0ocJFM"
  (read-keyset 'multi)
  (read-keyset 'multi))

(commit-tx)

(begin-tx "test rotation")
(env-keys
  [ "70c787fcfe6c6f4ec23d13c2e94682bc90952f7cec06c7dbac1c012b0b6678b9" ])
;; rotate to multi/multi
(define-namespace
  "D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4"
  (read-keyset 'multi)
  (read-keyset 'multi))
;; rotate to multi/single
(env-keys ["a","b"])
(define-namespace
  "D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4"
  (read-keyset 'multi)
  (read-keyset 'single))
(rollback-tx)


(begin-tx)
(env-exec-config [])
(env-keys
  [ "70c787fcfe6c6f4ec23d13c2e94682bc90952f7cec06c7dbac1c012b0b6678b9"
  , "a"
  , "b"
  ]
  )

(namespace "D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4")
(module modK G
  (defcap G () true)
  (defun f () 1))

(namespace "dDxw31bRij5gA0Xvl_uDUOZMvrnkMscu-Ax-H0ocJFM")
(module modW G
  (defcap G () true)
  (defun f () 2))

(commit-tx)
(begin-tx)

(expect
  "k: principal namespaces work"
  1
  (D8cqMpEfqKF_tj8I0be5VDdoYfBvb5ax_tFZMoHvXU4.modK.f))

(expect
  "w: principal namespaces work"
  2
  (dDxw31bRij5gA0Xvl_uDUOZMvrnkMscu-Ax-H0ocJFM.modW.f))
